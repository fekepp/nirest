/*
 * Plugins
 */
plugins {
	id 'application'
	id 'distribution'
}




/*
 * Configuration
 */
 
// General
description = 'NIREST Devices'


// Applications plugin
mainClassName = 'net.fekepp.nirest.device.Main'


// JAR metadata
jar {
	manifest {
		attributes	'Implementation-Title': 'NIREST Devices',
					'Implementation-Version': version
	}
}




/*
 * Dependencies
 */
dependencies {
	
	
	/*
	 * Compile Dependencies
	 */
	
	//
	// Internal
	//
	compile project(':nirest-model')
	
	
	//
	// External
	//
	
	
	//
	// Local
	//
	
	// OpenNI/NiTE
	compile files(
		'../redistributables/OpenNI-Linux-x64-2.2.0.33/org.openni.jar',
		'../redistributables/NiTE-Linux-x64-2.2.0.11/com.primesense.nite.jar',
	)
	
	
	/*
	 * Test Dependencies
	 */
	
	//
	// Internal
	//
	
	
	//
	// External
	//
	
	
	//
	// Local
	//
	
	
}




/*
 * Distribution
 */
distributions {
	
	main {
		
		contents {
			
			into('red') {
			
				from('../redistributables/OpenNI-Linux-x64-2.2.0.33') {
					exclude '**/OpenNI2', '**/*.ini', '**/*.jar'
				}
				
				from('../redistributables/NiTE-Linux-x64-2.2.0.11') {
					exclude '**/*.org.ini', '**/*.jar'
				}
				
			}
			
			into('red/OpenNI2-FreenectDriver') {
				
				from('../redistributables/OpenKinect-OpenNI2-FreenectDriver-0.5.3')
				
			}
			
			into('red') {
				
				from('../redistributables/OpenNI-Linux-x64-2.2.0.33/OpenNI.ini')
				
				filter {
					line -> line.replace(
						'Repository=../OpenKinect-OpenNI2-FreenectDriver-0.5.3',
						'Repository=OpenNI2-FreenectDriver'
					)
				}
				
			}
			
		}
		
	}
	
}




/*
 * Start Scripts
 */
startScripts {
	
	doLast {
		
		unixScript.text =
			unixScript.text.replace(
				'exec',
				'# Required to let the JVM correctly locate native libraries' +
				'\n' +
				'export LD_LIBRARY_PATH="$APP_HOME/red"' +
				'\n\n' +
				'# Required to fix "current working directory" issue of native NiTe libraries' +
				'\n' +
				'cd "$APP_HOME/red"' +
				'\n\n\n' +
				'exec'
			)
		
		windowsScript.delete()
		
	}
	
}