/*
 * Plugins
 */
plugins {
	id 'application'
}




/*
 * Configuration
 */
 
// General
description = 'NIREST LD-Fu API'

// Applications plugin
mainClassName = 'net.fekepp.nirest.ldfu.Main'

jar {
	manifest {
		attributes	'Implementation-Title': 'NIREST LD-Fu',
					'Implementation-Version': version
	}
}




/*
 * Dependencies
 */
dependencies {
	
	
	/*
	 * Compile Dependencies
	 */
	
	//
	// Internal
	//
	
	// NIREST
	compile project(':nirest-device')
	
	
	//
	// External
	//
		
	// Linked Data-Fu		
	compile 'edu.kit.aifb.datafu:linked-data-fu-common:0.9.+'

	compile 'edu.kit.aifb.datafu:linked-data-fu-datasets:0.9.+'
	//compile(group: 'edu.kit.aifb.datafu', name: 'linked-data-fu-datasets', version: '0.9.+') {
	//	exclude(module: 'jetty-server')
	//	exclude(module: 'test-jetty-servlet')
	//	exclude(module: 'jersey-container-servlet')
	//}
	
	compile 'edu.kit.aifb.datafu:linked-data-fu-engine:0.9.+'
	compile 'edu.kit.aifb.datafu:linked-data-fu-evaluators:0.9.+'
	compile 'edu.kit.aifb.datafu:linked-data-fu-io:0.9.+'
	compile 'edu.kit.aifb.datafu:linked-data-fu-operators:0.9.+'
	
	compile 'edu.kit.aifb.datafu:linked-data-fu-standalone:0.9.+'
	//compile(group: 'edu.kit.aifb.datafu', name: 'linked-data-fu-standalone', version: '0.9.+') {
	//	exclude(module: 'jetty-server')
	//	exclude(module: 'test-jetty-servlet')
	//	exclude(module: 'jersey-container-servlet')
	//}
	

	//
	// Local
	//
	
	
	/*
	 * Test Dependencies
	 */
	
	//
	// Internal
	//
	
	
	//
	// External
	//
	
	
	//
	// Local
	//
	
	
}




/*
 * Distribution
 */
distributions {
	
	main {
		
		contents {
			
			into('red') {
			
				from('../redistributables/OpenNI-Linux-x64-2.2.0.33') {
					exclude '**/OpenNI2', '**/*.ini', '**/*.jar'
				}
				
				from('../redistributables/NiTE-Linux-x64-2.2.0.11') {
					exclude '**/*.org.ini', '**/*.jar'
				}
				
			}
			
			into('red/OpenNI2-FreenectDriver') {
				
				from('../redistributables/OpenKinect-OpenNI2-FreenectDriver-0.5.3')
				
			}
			
			into('red') {
				
				from('../redistributables/OpenNI-Linux-x64-2.2.0.33/OpenNI.ini')
				
				filter {
					line -> line.replace(
						'Repository=../OpenKinect-OpenNI2-FreenectDriver-0.5.3',
						'Repository=OpenNI2-FreenectDriver'
					)
				}
				
			}
			
		}
		
	}
	
}




/*
 * Start Scripts
 */
startScripts {
	
	doLast {
		
		unixScript.text =
			unixScript.text.replace(
				'exec',
				'# Required to let the JVM correctly locate native libraries' +
				'\n' +
				'export LD_LIBRARY_PATH="$APP_HOME/red"' +
				'\n\n' +
				'# Required to fix "current working directory" issue of native NiTe libraries' +
				'\n' +
				'cd "$APP_HOME/red"' +
				'\n\n\n' +
				'exec'
			)
		
		windowsScript.delete()
		
	}
	
}