/*
 * Plugins
 */
plugins {
	id 'eclipse'
	id 'distribution'
}




/*
 * Configuration
 */
description = 'NIREST'




/*
 * Projects
 */
allprojects {
	
	
	/*
	 * Configuration
	 */
	version = '0.0.1-SNAPSHOT'
	
	
}




/*
 * Subprojects
 */
subprojects {
	
	
	/*
	 * Plugins
	 */
	apply plugin: 'eclipse'
	apply plugin: 'java'
	
	
	/*
	 * Repositories
	 */
	repositories {
		mavenLocal()
		mavenCentral()
		flatDir {
			dirs 'libraries'
		}
	}
	
	
	/*
	 * Dependencies
	 */
	dependencies {
		
		
		/*
		 * Compile Dependencies
		 */
		
		//
		// Internal
		//
		
		
		//
		// External
		//
		
		// SLF4J
		compile 'org.slf4j:slf4j-api:1.7.+'
		compile 'org.slf4j:log4j-over-slf4j:1.7.+'
		compile 'org.slf4j:jcl-over-slf4j:1.7.+'
		compile('ch.qos.logback:logback-classic:1.1.+')
		
		// Common
		compile 'commons-io:commons-io:2.4'
		compile 'com.google.guava:guava:15.0'
		compile 'com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:1.4'
		
		
		//
		// Local
		//
		
		
		/*
		 * Test Dependencies
		 */
		
		//
		// Internal
		//
		
		
		//
		// External
		//
		
		// JUnit
		testCompile 'junit:junit:4.+'
		
		
		//
		// Local
		//
		
		
	}
	
	
}




/*
 * Distribution
 */
distributions {
	
	main {
		
		contents {
		
			into('bin') {
				from { project(':nirest-device').startScripts.outputs.files }
				from { project(':nirest-server').startScripts.outputs.files }
				fileMode = 0755
			}
			
			into('lib') {

				// Include JARs of NIREST Device with dependencies
				from project(':nirest-device').jar
				from project(':nirest-device').configurations.runtime
				
				// Include JARs of NIREST Model with dependencies
				from project(':nirest-model').jar
				from project(':nirest-model').configurations.runtime
				
				// Include JARs of NIREST API with dependencies
				from project(':nirest-api').jar
				from project(':nirest-api').configurations.runtime
				
				// Include JARs of NIREST Server with dependencies
				from project(':nirest-server').jar
				from project(':nirest-server').configurations.runtime
				
			}
			
			into('red') {
			
				from('redistributables/OpenNI-Linux-x64-2.2.0.33') {
					exclude '**/OpenNI2', '**/*.ini', '**/*.jar'
				}
				
				from('redistributables/NiTE-Linux-x64-2.2.0.11') {
					exclude '**/*.org.ini', '**/*.jar'
				}
				
			}
			
			into('red/OpenNI2-FreenectDriver') {
				
				from('redistributables/OpenKinect-OpenNI2-FreenectDriver-0.5.3')
				
			}
			
			into('red') {
				
				from('redistributables/OpenNI-Linux-x64-2.2.0.33/OpenNI.ini')
				
				filter {
					line -> line.replace(
						'Repository=../OpenKinect-OpenNI2-FreenectDriver-0.5.3',
						'Repository=OpenNI2-FreenectDriver'
					)
				}
				
			}
			
		}
		
	}
	
}
